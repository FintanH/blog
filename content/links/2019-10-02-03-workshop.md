# Principles Matter - Workshop

In this workshop we will use types to find and fix bugs in a software
module.

Open [the workshop exercise](https://d61-live-2019-env.qfpl.io) in
your browser. Ellie is an online sandbox for the
[Elm](https://elm-lang.org/) programming language, so we can get
started without installing a compiler or other development tools.

The module we'll work on in this workshop implements "line items",
like you might see in a shopping cart or order management software. A
line item is made up of the following pieces of information:

* The SKU (Stock Keeping Unit)
* Quantity
* The base price (before tax)
* The tax rate

On the right panel, you will see a few example line items, and a list
of test cases. Some tests pass, some fail, and some pass when they
shouldn't - they compute the expected answer in the wrong way. We will
work through them all during this workshop, but first we need to learn
how to read Elm.


## Elm in 5 Minutes

### Comments

Lines that start with `--` are comments - they are only for people
reading the code.

### Built-in Types

Types are how Elm tells apart different kinds of things: for example,
a number is not the same sort of thing as text. Elm has some built-in
types, and we can use those to define types of our own.

The built-in type we will use today are:

* `String`, the type for sequences of text, which are written inside a
  pair of double quotes.

  - `"Hello"` is a `String`.
  - `"Goodbye"` is also a `String`.
  - `'Nope'` is not a `String`, because it used single quotes `'`.

* `Int`, the type for whole numbers. (The name is short for 'integer'.)

  - `2` is an `Int`.
  - `-5` is an `Int`.

* `Float`, the type for decimal numbers. (The name comes from
  'floating point', which describes its internal
  implementation. Generally not suitable for currency, but we'll
  handwave past that today.)

  - `3.14` is a `Float`.
  - `-2.18` is a `Float`.

### Defining Types

The keyword `type` defines a new type. We use it to introduce a type
for line items:

```elm
type LineItem
    = MakeLineItem String Int Float Float
```

This says that there is now a type called `LineItem`, and you can
construct values of that type by applying `MakeLineItem` to a `String`
and an `Int` and two `Float`s.

Or, in code, that declaration now means that:

* `MakeLineItem "SKU-123" 1 2.0 0.2` is a `LineItem`
* `MakeLineItem "NO-TAX" 2 4.0 0` is a `LineItem`
* `MakeLineItem "SKU-456" 3 4.0 0.1` is a `LineItem`

### Defining Functions

Functions are defined in two parts. Let's define a function called
`sku` which turns a `LineItem` into a `String` representing only its
SKU. (We sometimes say it _accepts_ a `LineItem` and _returns_ a
`String`.) The first thing we have to define is a type signature:

```elm
sku : LineItem -> String
```

This says that `sku` is a function from `LineItem` to `String`. (For
functions which take multiple inputs, use additional arrows like in
`applyTaxRate : Float -> Float -> Float`. This looks odd, but is done
for good reasons.)

The other half of the definition looks like this:

```elm
sku (MakeLineItem s _ _ _) =
    s
```

The first line (before the `=`) is a _pattern match_, where we inspect
the structure of the thing given to the function (its _argument_) and
give names to the parts we care about. Each argument to `MakeLineItem`
could be given a name, and because we are only interested in naming
the first argument to `MakeLineItem` we use a blank pattern
(written as underscore - `_`) to give the correct shape.

The part after the `=` describes the result of the function. Here, it
is just `s` - the sku we extracted from the `MakeItem` argument.

### Using Functions

To call a function, list the arguments, one after another:

```elm
-- Given:
double : Int -> Int
double x = x * 2

pythagoras : Float -> Float -> Float
pythagoras x y = sqrt (x * x + y * y)

double 3        -- is 6
pythagoras 3 4  -- is 5
```

If the arguments are themselves complicated, surround them in parens `()`:

```elm
pythagoras (1 + 2) (2 * 2)
```

We can explore how this computation works by repeatedly replacing
functions with their values:

```elm
pythagoras (1 + 2) (2 * 2)
= pythagoras 3 (2 * 2)
= pythagoras 3 4
= sqrt (3 * 3 + 4 * 4)   -- Because x = 3 and y = 4 in our call to pythagoras
= sqrt (9 + 4 * 4)
= sqrt (9 + 16)
= sqrt 25
= 5
```

Part of the payoff of adopting Functional Programming is that you can
apply this thinking _everywhere_ in your programs. Understanding how
computations proceed in other programming languages is not as easy.

## Your First Bugfix

The tests are defined in a list starting at line 118.

Let's start with the first test - the result of calling the
`lineItemPrice` function with a line item that has quantity zero is
wrong. Look at the function definition for `lineItemPrice` (line 59) -
can you see the bug?

If you think you've fixed the bug, press the **Compile** button in the
top middle of the Ellie screen to check your program and re-run the
tests.

<details>
    <summary>Hint</summary>

`lineItemPrice` is not considering the quantity in the `LineItem`.
</details>

<details>
    <summary>Solution</summary>

```elm
itemPrice : LineItem -> Float
itemPrice item =
    applyQuantity (quantity item) (applyTaxRate (basePrice item) (taxRate item))
```
</details>


## Bugfixing With Types

If you scroll through the code you've been working on, you'll find a
lot of functions that accept and return `Floats`, but we're actually
talking about a few different types of numbers: 'base prices', 'tax
rates' and 'prices after tax'. Let's define some types to track those.
Add the following type definitions:

```elm
type BasePrice = MakeBasePrice Float
type TaxRate = MakeTaxRate Float
type Price = MakePrice Float
```

Also change the type of `LineItem` to use these new types:

```elm
type LineItem
    = MakeLineItem String Int BasePrice TaxRate
```

Now if you press **Compile**, you will see many _Type Mismatch_
errors. The next task is to work through through these errors and
update the functions to make everything compile again. You will also
need to update the `LineItem`s in `lineItems` and `tests`.

<details>
    <summary>Hint</summary>

These are the new type signatures for your functions:

* `basePrice : LineItem -> BasePrice`
* `taxRate : LineItem -> TaxRate`
* `applyQuantity : Int -> Price -> Price`
* `applyTaxRate : TaxRate -> BasePrice -> Price`
* `lineItemPrice : LineItem -> Price`
* `showBasePrice : BasePrice -> String`
* `showTaxRate : TaxRate -> String`
* `showPrice : Price -> String`
</details>

You should notice that the implementation of `applyTaxRate` is wrong -
adding the tax rate to the base price contradicts the description of
what a tax rate means in the opening comment. Following the type
errors has pointed us to this defect. It is time to fix this.

<details>
    <summary>Hint</summary>

The correct way to apply the tax rate is _basePrice * (1 + taxRate)_.
</details>

<details>
    <summary>Solution</summary>

```elm
applyTaxRate : TaxRate -> BasePrice -> Price
applyTaxRate (MakeTaxRate rate) (MakeBasePrice price) =
    MakePrice (price * (1 + rate))
```
</details>

The type errors point us at another bug in `lineItemPrice`: the base
price and tax rate arguments are swapped around! When all the
arguments were `Float`, this passed unnoticed, but now that we are
speaking precisely about the types in play, the Elm compiler has
brought the bug to our attention.


## Conclusions

So what have we learned from this?

First off, a number is not just a number: it could be a `BasePrice`,
or a `TaxRate`, or a `Price`. By giving explicit names to the
different types of number, we drastically limited the ways our
functions could be combined, and this unearthed a number of bugs.

Imagine the payoff if you applied this to a larger software project!

You can apply some of these techniques in mainstream languages, but
functional programming languages make it much easier. Many coding
standards require each type to be a separate file, which makes adding
types an annoying process for developers. In Elm and other functional
programming languages, a new type is often one line of code.
